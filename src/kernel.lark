program: "(" "program" "(" parameters* ")" expr ")"

parameters: identifier*

expr: int                                                           -> int_expr
    | "(" "+" expr expr ")"                                         -> add_expr
    | "(" "-" expr expr ")"                                         -> subtract_expr
    | "(" "*" expr expr ")"                                         -> multiply_expr
    | "(" "let" "(" "[" identifier expr "]" ")" expr ")"            -> let_expr
    | identifier                                                    -> var_expr
    | (true | false)                                                -> bool_expr
    | "(" "if" expr expr expr ")"                                   -> if_expr
    | "(" "<" expr expr ")"                                         -> less_than_expr
    | "(" "==" expr expr ")"                                        -> equal_to_expr
    | "(" ">=" expr expr ")"                                        -> greater_than_or_equal_to_expr
    | unit                                                          -> unit_expr
    | "(" "cell" expr ")"                                           -> cell_expr
    | "(" "get" expr ")"                                            -> get_expr
    | "(" "set" expr expr ")"                                       -> set_expr
    | "(" "do" expr expr ")"                                        -> do_expr
    | "(" "while" expr expr ")"                                     -> while_expr

int: INT
true: TRUE
false: FALSE
unit: UNIT
identifier: IDENTIFIER

INT: ("0".."9")+

TRUE: "#t"
FALSE: "#f"

UNIT: "#u"

IDENTIFIER: /[^\W\d]\w*/

WS: /[ \t\f\r\n]/+

%ignore WS

