expr: int                                                           -> int_expr
    | "(" "+" expr expr ")"                                         -> int_add_expr
    | "(" "-" expr expr ")"                                         -> int_subtract_expr
    | "(" "*" expr expr ")"                                         -> int_multiply_expr
    | "(" "let" "(" "[" identifier expr "]" ")" expr ")"            -> let_expr
    | identifier                                                    -> var_expr
    | (true | false)                                                -> bool_expr
    | "(" "if" expr expr expr ")"                                   -> if_expr
    | "(" "<" expr expr ")"                                         -> less_than_expr
    | "(" "==" expr expr ")"                                        -> equal_to_expr
    | "(" ">=" expr expr ")"                                        -> greater_than_or_equal_to_expr

int: INT
true: TRUE
false: FALSE
identifier: IDENTIFIER

INT: ("0".."9")+

TRUE: "#t"
FALSE: "#f"

IDENTIFIER: /[^\W\d]\w*/

WS: /[ \t\f\r\n]/+

%ignore WS

